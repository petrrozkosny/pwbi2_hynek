dim_datumy = 
ADDCOLUMNS(
    -- funkce CALENDARAUTO() vraci tabulku se sloupcem [Date]
    CALENDARAUTO(),
    -- nize pridavame sloupce do tabulky vracene funkci CALENDARAUTO()
    -- kdy pocitame hodnoty ze sloupce [Date]
    "rok",YEAR([Date]),
    "kvartal",QUARTER([Date]),
    "mesic",MONTH([Date]),
    "den",DAY([Date]))


_________________________________

01 rocni srazky = 

SUMMARIZE(
    -- tabulka, jejiz sloupce agregujeme
    data,
    -- sloupce tabulky data, podle kterych agregujeme
    data[rok],data[mesic],
    -- nove pridavane sloupce, ktere agregujeme dle [rok] a [mesic]
    "mesicni srazky",SUM(data[Srazky]),
    "maximalni teplota",max(data[Teplota])
)


________________________________________________
01 suma srazek = 
/*
Respektuji filtry vizualu, nepridavam dalsi, tj. pouzivam zakladni agregacni
funkci, ktera agreguje sloupec tabulky filtrovane vizualem.
*/
SUM(data[Srazky])

_______________________________________________

02 suma srazek ruzyne = 
/*
Respektuji filtry vizualu a pridavam dalsi filtr, a to na
sloupec [lokalita] tabulky data
*/
CALCULATE(
    --krok4: Scitame sloupec [Srazky] tabulky vracene funkci FILTER()
    SUM(data[Srazky]),
    -- krok3: Funkce FILTER() vraci tabulku
        FILTER(
    --krok1: Funkce FILTER() prebira tabulku data filtrovanou
    -- vizualem.
    data,
    --krok2: Funkce FILTER() nastavuje filtr na sloupec [Lokalita]
    data[Lokalita] = "RUZYNE"))
    
 _________________________________________________
 
 03 suma srazek vsechny roky = 
/*
Potrebujeme se zbavit filtru nad sloupcem [Rok]. Vytvarime
si kopii tabulky data zbavene vsech filtru a tu potom agregujeme.
*/

CALCULATE(
    --krok2: Funkce SUM() agreguje sloupec [Srazky] tabulky
    -- vracene funkci ALL()
    SUM(data[Srazky]),
    --krok1: Funkce ALL() vraci kopii tabulky data zbavenou
    -- vsech filtru
    ALL(data))
    
_________________________________________________________
04 suma srazek predchozi rok = 
/*
Potrebujeme "vypnout" filtr nad rokem a nastavit si vlastni filtr
nad stejnym sloupcem, pricemz hodnotu filtru si dopocitavame dynamicky
*/
CALCULATE(
    --krok4: Funkce SUM() scita sloupec [Srazky] tabulky vracene funkci FILTER()
    SUM(data[Srazky]),
    --krok3: Funkce FILTER() vraci tabulku
    FILTER(
        --krok1: Funkce ALL() vraci kopii tabulky data zbavenou vsech filtru
        ALL(data),
    --krok2: Funkce FILTER() nastavuje filtr na sloupec [rok] tabulky
    -- vracene funkci ALL(), pricemz hodnotu filtru si pocitame
    -- z originalni, tj. zafiltrovane, tabulky za pomoci funkce SELECTEDVALUE()
    data[rok] = SELECTEDVALUE(data[rok])-1)
)


_____________________________________________

14 suma srazek mesic 6 = 
-- takto ne
VAR varianta1 = 
CALCULATE(
    SUM(data[Srazky]),
    FILTER(data,data[mesic] = 6))
-- takto ano, kdyz nemuzu jinak
VAR varianta2 = 
CALCULATE(
    SUM(data[Srazky]),
    FILTER(
        ALL(dim_datumy[mesic]),
        dim_datumy[mesic] = 6))
-- takto ano, timto zacinam
VAR varianta3=
    CALCULATE(
        SUM(data[Srazky]),
        dim_datumy[mesic] = 6)
return
varianta3


_______________________________________________

31 maximalni a minimalni rocni srazky = 

VAR tabulka = 
SUMMARIZE(ALL(data),
data[rok],"rocni srazky",SUM(data[Srazky]))

VAR minimalni_rocni_srazky = 
ROUND(
MINX(tabulka,[rocni srazky]),2)

VAR maximalni_rocni_srazky = 
round(
MAXX(tabulka,[rocni srazky]),2)

RETURN
SWITCH(TRUE(),
ROUND(
SUM(data[Srazky]),2) = maximalni_rocni_srazky,"CYAN",
ROUND(SUM(data[Srazky]),2) = minimalni_rocni_srazky,"GREY")



______________________________________________________________________

38 suma srazek predchozi obdobi = 
VAR suma_srazek_predchozi_rok = CALCULATE(
    SUM(data[Srazky]),
    dim_datumy[rok] = SELECTEDVALUE(dim_datumy[rok]) - 1)

VAR maximalni_rok = CALCULATE(MAX(dim_datumy[rok]),all(dim_datumy[rok]))
VAR minimalni_rok = CALCULATE(MIN(dim_datumy[rok]),all(dim_datumy[rok]))

VAR suma_srazek_maximalni_rok = 
    CALCULATE(
        SUM(data[Srazky]),
        dim_datumy[rok] = maximalni_rok)

VAR suma_srazek_minimalni_rok = 
    CALCULATE(
        SUM(data[Srazky]),
        dim_datumy[rok] = minimalni_rok)
RETURN
SWITCH(TRUE(),
SELECTEDVALUE(typ_roku[typ_roku]) = "predchozi rok",suma_srazek_predchozi_rok,
SELECTEDVALUE(typ_roku[typ_roku]) = "maximalni rok",suma_srazek_maximalni_rok,
suma_srazek_minimalni_rok)
